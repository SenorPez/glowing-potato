buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath('org.springframework.boot:spring-boot-gradle-plugin:1.5.6.RELEASE')
        classpath('org.asciidoctor:asciidoctor-gradle-plugin:1.5.9.2')
    }
}

plugins {
    id 'java'
    id 'jacoco'
    id 'idea'
    id 'org.springframework.boot' version '1.5.6.RELEASE'
    id 'org.asciidoctor.convert' version '1.5.9.2'
}

group 'com.senorpez.trident'
version '0.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

springBoot {
    executable = true
}

idea {
    module {
        name = 'api'
    }
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-hateoas', version: '1.5.6.RELEASE'
    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.hamcrest', name: 'hamcrest-all', version: '1.3'
    testCompile group: 'com.jayway.jsonpath', name: 'json-path-assert', version: '2.4.0'
    testCompile group: 'com.jayway.restassured', name: 'json-schema-validator', version: '2.9.0'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '2.24.5'
    testCompile group: 'org.springframework', name: 'spring-test', version: '4.3.10.RELEASE'
    testCompile group: 'org.springframework.boot', name: 'spring-boot-test', version: '1.5.6.RELEASE'
    testCompile group: 'org.springframework.restdocs', name: 'spring-restdocs-core', version: '1.2.1.RELEASE'
    testCompile group: 'org.springframework.restdocs', name: 'spring-restdocs-mockmvc', version: '1.2.1.RELEASE'
    asciidoctor group: 'org.springframework.restdocs', name: 'spring-restdocs-asciidoctor', version: '1.2.1.RELEASE'
}

ext {
    snippetsDir = file('build/generated-snippets')
}

test {
    outputs.dir snippetsDir
}

asciidoctor {
    inputs.dir snippetsDir
    dependsOn test
    attributes 'snippets': file('build/generated-snippets')
}

jar {
    dependsOn asciidoctor
    from ("${asciidoctor.outputDir}/html5") {
        into 'static/docs'
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = false
    }
}